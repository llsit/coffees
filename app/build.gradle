apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.cmu.nuts.coffee9"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 4
        versionName "2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        lintOptions {
            disable 'MissingTranslation'
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
        }
    }
    productFlavors {
    }

}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        mavenCentral()
    }
}

def dependence(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def android = dependence {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
}

def google = dependence {
    implementation 'com.google.android.gms:play-services-nearby:11.8.0'
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-location:11.8.0'
}

def firebase = dependence {
    implementation 'com.google.firebase:firebase-auth:11.8.0'
    implementation 'com.google.firebase:firebase-database:11.8.0'
    implementation 'com.google.firebase:firebase-storage:11.8.0'
    implementation 'com.firebaseui:firebase-ui-database:3.2.2'
}

def facebook = dependence {
    implementation 'com.facebook.android:facebook-login:4.29.0'
    implementation 'com.facebook.android:facebook-share:[4,5)'
}

def library = dependence {
    implementation 'com.code-troopers.betterpickers:library:3.1.0'
    implementation 'com.wdullaer:materialdatetimepicker:3.4.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'org.parceler:parceler-api:1.1.9'
    implementation 'com.akexorcist:localizationactivity:1.2.2'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'me.zhanghai.android.materialratingbar:library:1.2.0'
    implementation 'com.github.ivbaranov:materialfavoritebutton:0.1.4'
    implementation 'com.github.kobakei:MaterialFabSpeedDial:1.2.0'
    implementation 'com.github.drawers:SpinnerDatePicker:1.0.6'
    implementation 'com.facebook.android:facebook-android-sdk:[4,5)'
    implementation 'com.algolia:algoliasearch-android:3.+'
    implementation 'com.github.codbking:PickTime:v1.0.1'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation('io.nlopez.smartlocation:library:3.3.3') {
        transitive = false
    }
    annotationProcessor 'org.parceler:parceler:1.1.9'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}

def image = dependence {
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.esafirm.android-image-picker:imagepicker:1.12.0'
}

def test = dependence {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    android()
    google()
    firebase()
    facebook()
    image()
    library()
    test()
}
apply plugin: 'com.google.gms.google-services'
